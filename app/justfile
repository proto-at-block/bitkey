gradle := "../bin/gradle"
gradle_android := "../bin/gradle --project-dir android"
gradle_shared := "../bin/gradle --project-dir shared"
xcode_clt_path := "/Library/Developer/CommandLineTools"

[private]
default:
  just --list

[private]
list:
  just --list

# Update git submodules
submodules:
  git submodule update --init --recursive

# Run Gradle profiler to measure build times
gradle-profiler:
 # GRADLE_PROFILER environment variable is used as a tag in Build Scans.
 GRADLE_PROFILER=true gradle-profiler --benchmark --scenario-file gradle/profiler.scenarios --gradle-version $(bin/hermit info gradle | awk '/^Root/ { print $2 }')

# Create git tag and branch for Team App release
create-team-branch version-number:
  git tag --annotate {{version-number}} --message {{version-number}}
  git checkout -b internal-release/{{version-number}}

# Create git tag and branch for Beta App release
create-beta-branch version-number:
  git tag --annotate {{version-number}} --message {{version-number}}
  git checkout -b release/{{version-number}}

# Build and run CLI app
cli:
  just cli-build
  just cli-run

# Build CLI app
cli-build:
  {{gradle}} jar -p shared/cli

# Run CLI app
cli-run:
  java -jar shared/cli/_build/libs/cli-jvm.jar

# Builds and installs debug flavor of the Android app
android-app-install:
  {{gradle}} installDebug -p android/app

# Clear our Android app's data and cache
android-app-clear:
  adb shell pm clear world.bitkey.app

# Uninstall Android app
android-app-uninstall:
  adb uninstall world.bitkey.app

# Verify SqlDelight schemas and migrations
test-unit-database:
  {{gradle_shared}} verifySqlDelightMigration verifySqlDelightMigrationForeignKeyCheck

# Run unit tests targeting JVM
test-unit-jvm:
  {{gradle_shared}} jvmTest

# Run unit tests on host machine targeting Android (with platform stubs)
test-unit-android-host:
  {{gradle}} testDebugUnitTest

# Run unit tests on iOS simulator
test-unit-ios-simulator:
  {{gradle_shared}} iosTest

# Run integration tests targeting JVM
test-integration-jvm:
  {{gradle_shared}} cleanJvmIntegrationTest jvmIntegrationTest

# Run integration tests targeting all platforms - currently only JVM is supported
test-integration-all:
  {{gradle_shared}} cleanJvmIntegrationTest jvmIntegrationTest

# Run unit tests targeting all platforms
test-unit-all:
  {{gradle}} jvmTest testDebugUnitTest iosTest verifySqlDelightMigration

# Run unit and integration tests targeting all platforms
test-all:
  just test-unit-all
  just test-integration-all

# Record and update baseline UI snapshots for Android
android-snapshots-record:
  {{gradle_android}} recordPaparazziDebug

# Verify UI snapshots against the baseline for Android
android-snapshots-verify:
  {{gradle_android}} verifyPaparazziDebug

# Record and update baseline UI snapshots for iOS
ios-snapshots-record:
  #!/bin/bash
  just ios-snapshot-prework
  export SNAPSHOT_TESTS_RECORD=true
  xcodebuild test -project 'ios/Wallet.xcodeproj' -scheme 'WalletSnapshotTests' -destination 'platform=iOS Simulator,name=iPhone 15 Pro Max,OS=17.2'

# Verify UI snapshots for iOS
ios-snapshots-verify:
  #!/bin/bash
  just ios-snapshot-prework
  export SNAPSHOT_TESTS_RECORD=false
  xcodebuild test -project 'ios/Wallet.xcodeproj' -scheme 'WalletSnapshotTests' -destination 'platform=iOS Simulator,name=iPhone 15 Pro Max,OS=17.2'

[private]
ios-snapshot-prework:
  #!/bin/bash
  just install-xcode-clt
  sim=$(xcrun simctl list | grep 'com.apple.CoreSimulator.SimRuntime.iOS-17-2' && echo true || echo false)
  if [ "$sim" == "false" ]; then
    echo "You must install the iOS 17.2 simulator runtime to record and verify snapshot tests. See the docs https://docs.wallet.build/guides/mobile/ui/framework/#updating-the-snapshots"
    exit 1;
  fi
  just xcodegen

# Configure and onboard local environment:
onboard: install-git-lfs install-xcode-clt install-jetbrains-toolbox submodules install-xcode-kmp-plugin install-docker install-aws-creds sync-aws-creds list-env-vars install-coreutils

# Generate Xcode project (for both device and simulator targets)
xcodegen:
  cd ios/ && xcodegen
  just reconstruct-ios-package-resolved-file

[private]
install-xcode-clt:
  xcode-select --install || true # "fails" if Xcode CLT already installed
  @echo "Waiting for Xcode Command Line Tools to be installed..."
  @while ! {{xcode_clt_path}}/usr/bin/clang --version 2> /dev/null; do sleep 1; done
  @echo "Xcode Command Line Tools are installed."

[private]
install-xcode-kmp-plugin:
  brew install xcode-kotlin
  xcode-kotlin install
  @echo "Xcode KMP plugin is installed."

[private]
install-coreutils:
  brew install coreutils
  @echo "coreutils is installed."

[private]
install-aws-creds:
  brew install aws-creds
  @echo "AWS Creds is installed."

[private]
install-git-lfs:
  git lfs install --local
  @echo "Git LFS installed."

[private]
sync-aws-creds:
  aws-creds sync

[private]
install-jetbrains-toolbox:
  brew install --cask jetbrains-toolbox --require-sha
  @echo "JetBrains Toolbox is installed."

[private]
install-docker:
  command -v docker || brew install --cask docker --require-sha
  @echo "Docker is installed."

[private]
list-env-vars:
  @echo "Set these environment variables OR add them to  ~/.zprofile:"
  @echo 'export ANDROID_HOME="$HOME/Library/Android/sdk"'
  @echo 'export ANDROID_NDK_VERSION="25.2.9519653"'
  @echo 'export ANDROID_BUILD_TOOLS_VERSION="34.0.0"'
  @echo 'export ANDROID_NDK_ROOT=$ANDROID_HOME/ndk/$ANDROID_NDK_VERSION'
  @echo 'export PATH=$ANDROID_HOME/tools:$PATH'
  @echo 'export PATH=$ANDROID_HOME/build-tools/$ANDROID_BUILD_TOOLS_VERSION:$PATH'
  @echo 'export PATH=$ANDROID_HOME/platform-tools:$PATH'
  @echo 'export JAVA_HOME=$(/usr/libexec/java_home -v 17)'
  @echo 'export PATH=$PATH:$JAVA_HOME'

# Run Detekt code analysis on Kotlin sources
detekt:
  {{gradle}} detektAll --continue

# Run Detekt code analysis on `app/android` code - this does not cover Android source sets in KMP
detekt-android:
  {{gradle_android}} detektAll --continue

# Run Detekt code analysis on all KMP code
detekt-kmp:
  {{gradle_shared}} detektAll --continue

# Run Ktlint, reformat code when possible, fail on violations.
ktlint-format:
  just ktlint-validate --format

# Run Ktlint and fail on violations.
ktlint-validate ARGS="":
  ktlint './**/*.kt' './**/*.kts' '!**/_build/**' '!**/core/target/**' {{ARGS}}

# Delete XCode cached build intermediates (Derived Data)
dxdd:
  rm -rfI "/Users/$USER/Library/Developer/Xcode/DerivedData"

# Generate Android and iOS design tokens
update-design-tokens:
  cd style && npm run build 

# Generates .db schema for databases
generate-db-schema:
  {{gradle}} generateCommonMainBitkeyDatabaseSchema generateCommonMainBitkeyDebugDatabaseSchema

# Verifies validity of the schema and runs migration tests
verify-db-schema:
  {{gradle}} verifySqlDelightMigration

# Run Maestro End-to-End Integration Tests
maestro os:
  if [[ "{{os}}" == "ios" ]]; then \
    maestro test -e APP_ID=world.bitkey.dev maestro-test --include-tags=ios; \
  elif [[ "{{os}}" == "android" ]]; then \
    maestro test -e APP_ID=world.bitkey.debug maestro-test --include-tags=android; \
  fi


# Ensure that correct Gradle version is used, see script file for details.
validate-gradle-version:
  ./scripts/validate-gradle-version.sh

start-backend:
  just -f ../server/justfile start-backend

stop-backend:
  just -f ../server/justfile stop-backend

restart-backend:
  just stop-backend && just start-backend 

get-regtest-funds address amount:
  just -f ../server/justfile get-regtest-funds {{ address }} {{ amount }}

update-gradle-lockfile:
  {{gradle}} :updateDependencyLockFile :build-logic:updateDependencyLockFile -Pbuild.wallet.kmp.iosEnableAllTargets=true -Pbuild.wallet.dependency-locking.is-enabled=false

verify-gradle-lockfile:
  {{gradle}} verifyDependencyLockFile :build-logic:verifyDependencyLockFile -Pbuild.wallet.kmp.iosEnableAllTargets=true

resolve-ios-packages: xcodegen
  xcodebuild -resolvePackageDependencies -project ios/Wallet.xcodeproj

reconstruct-ios-package-resolved-file:
  mkdir -p ios/Wallet.xcodeproj/project.xcworkspace/xcshareddata/swiftpm
  cp ios/Package.lock ios/Wallet.xcodeproj/project.xcworkspace/xcshareddata/swiftpm/Package.resolved

# Use only if the Package.resolved was already updated (for example by Xcode)
fast-update-ios-lockfile:
  cp ios/Wallet.xcodeproj/project.xcworkspace/xcshareddata/swiftpm/Package.resolved ios/Package.lock

update-ios-lockfile: resolve-ios-packages fast-update-ios-lockfile

# Use only if the Package.resolved was already updated (for example by Xcode)
fast-verify-ios-lockfile:
  #!/usr/bin/env bash
  diff_output=$(diff ios/Package.lock ios/Wallet.xcodeproj/project.xcworkspace/xcshareddata/swiftpm/Package.resolved)
  diff_exit_code=$?
  if [ $diff_exit_code -ne 0 ]; then
    echo "Resolved packages do not match the lock file." >&2
    echo "Consider updating the lockfile using \"just fast-update-ios-lockfile\"." >&2
    echo "$diff_output" >&2
    exit $diff_exit_code
  fi

verify-ios-lockfile: resolve-ios-packages fast-verify-ios-lockfile

verify-android-apk path-to-bitkey-repository-directory path-to-build-directory app-package-name:
  verifiable-build/android/verification/verify-android-apk {{path-to-bitkey-repository-directory}} {{path-to-build-directory}} {{app-package-name}}

generate-state-machine-graph machine_name='' excludes='' output_file='':
  gradle :shared:state-machine-ui-public:generateStateMachineDiagram -PmachineName={{machine_name}} -PexcludingMachineNames={{excludes}} -PoutputFile={{output_file}}

generate-entire-state-machine-graph output_file='':
  gradle :shared:state-machine-ui-public:generateStateMachineDiagram -PoutputFile={{output_file}} -PoverridePermanentExcludes=true
